---
title: 运营活动营收总结
tags: Work Design

---
### MMORPG类

#### 投放框架 - 数值设计

主要付费来源 集训-商城-福利

| 类型 | 人民币 | 信用点 | 金条 |
| :---: | :---: | :---: | :---: |
| XX | 1 | 12 | 1200 |

整体投放 - 回收， 稳定游戏内物价
1：10000  -> 1:8000

付费能力 - 付费等级 - 付费投放预期

付费等级预期X通常付费人数

##### 活跃活动 - 福利活动

投放价值比较低，主要配合日常集训完成活跃度，提高DAU用的，最终效果还是要看次留等数据
是活动代币的主要产出来源

签到、游戏内任务、特殊活动玩法（主要）

##### 付费活动 - 奖池类活动 
时装奖池、武器奖池、幸运礼盒、载具奖池等主要营收抽奖

抽奖概率-数值设计

| 编号 | 名称 | 说明 |
| --- | --- | ---|
| H | item |
| I | id |
| J | 数量 |
| K | 限购 |
| L | 前置配方 |
| M | 前置道具 |
| N | 展示组 |
| O | 公共组 |
| P | 组价值折扣 |
| Q | 价值系数 | 游戏中投放导致数值需求上的一定价值平衡
| R | 序号 | 一个公共组的序号
| S | 单价(RMB) |
| T | 单价（信） |
| U | 总价（信） |
| V | 定价概率 | 单抽信用点价格/总价, 理论上玩家所花一定钱能够抽到的概率
| W | ***系数*** | 调整大数的计算，如果展示组1的道具系数很小，公共组来平衡概率
| X | 内部概率 | 一个展示组的内部的概率,指稀有&普通, 第一个乘了系数, 第二个进行权重
| Y | 大数概率 | 分组2是按照分组1的剩余概率来内部划分
| Z | ***权重*** |
| AA | 最终的概率 |
| AB | 手动权重 | 是否有保底
| AC | 期望权重 | 用来计算目标概率的权重
| AD | 初始权重 |
| AE | 累加权重 |
| AF | 调节变量 | 初始权重/累加权重
| AG | 目标概率 |
| AH | 期望次数 | 实际单个玩家抽的预期次数
| AI | 定价次数 |
| AJ | 验算保底 |
| AK | 手动保底 | 在验算保底的基础上，手动调整保底次数，一般会提高低价值的保底
| AL | 实际保底 |
| AM | 保底概率 | 概率应该小于目标概率
| AN | 浮动保底 |
| AO | 抽取门槛 |
| AP | 冷却次数 |
| AQ | 填写冷却次数 |
| AR | 首次获得 |
| AS | 首次天数 |
| AT | 表格填写 |


``` excel

'固定值'
$B$3 单抽价格(信)
$B$4 必得倍数 - 保底次数  1.5
$B$5 浮动比例 手动影响保底次数 0.05

'SUM(单价X数量X大数概率) / (单抽信用点价格/12)'
返利比=SUMPRODUCT(S2:S29,J2:J29,Y2:Y29)/(B3/12)
返利比相当于 单位消费，实际获得返利比X单位消费的 收益 (计算收益)

单价  数量
价值系数：游戏中投放导致数值需求上的一定价值平衡
组价值折扣：一般为1-同组道具数量, 不填单人成组，避免系数太小
公共组：0 单人成组  
系数：调整大数的计算，如果展示组1的道具系数很小，公共组来平衡概率

'IFERROR(IF(公共组<1,单价*总价,IF(序号=1,IF(组价值折扣="","",数量*组价值折扣*单价),""))*IF(价值系数="",1,价值系数),"")'
总价=IFERROR(IF(O4<1,T4*J4,IF(R4=1,IF(P4="","",J4*P4*T4),""))*IF(Q4="",1,Q4),"")

'单抽信用点价格/总价'
定价概率=IFERROR(IF(U6="","",$B$3/U6),"")

'IF(OR(总价="",展示组=2),"",IF(IF(限购="",999,限购)*U2/(单抽价格/12*14*50)>1,1,IF(限购="",9999999,限购)*总价/(单抽价格*14*50)))'
系数=
IF(OR(U2="",N2=2),"",IF(IF(K2="",999,K2)*U2/($B$3/12*14*50)>1,1,IF(K2="",9999999,K2)*U2/($B$3*14*50)))

'IF(定价概率="","",IF(展示组=1,定价概率*系数,定价概率/SUMIF(N:N,2,V:V))) 展示组2的定价概率的和'
内部概率=IF(V8="","",IF(N8=1,V8*W8,V8/SUMIF(N:N,2,V:V)))

'IF(展示组=1,IF(定价概率="","",IF(展示组=1,系数*定价概率,SUMIF(N:N,1,$X:$X)*内部概率)),IF(定价概率="","",(1-SUMIF(N:N,1,$Y:$Y))*内部概率))  X 内部概率  Y 大数概率'
大数概率=IF(N2=1,IF(V2="","",IF(N2=1,W2*V2,SUMIF(N:N,1,$X:$X)*X2)),IF(V2="","",(1-SUMIF(N:N,1,$Y:$Y))*X2))

'IF(大数概率="",IF(序号=1,$U$2*$Z$2/(单价*IF(价值系数="",1,价值系数)*数量),""),ROUND(大数概率*100000,0)) U2 是最贵物品的总价 Z2 是最贵物品的权重'
权重=IF(Y2="",IF(R2=1,$U$2*$Z$2/(T2*IF(Q2="",1,Q2)*J2),""),ROUND(Y2*100000,0))

'权重占比'
最终概率=IFERROR(Z2/SUM(Z:Z),"")

'IF(手动权重="",INT(IF(序号>1,上一个道具的期望权重,IF(序号=1,权重/COUNTIF(O:O,O2),权重))),手动权重) 权重/COUNTIF(O:O,O2) O是公共组，即同公共组等分权重'
期望权重=IF(AB2="",INT(IF(R2>1,AC1,IF(R2=1,Z2/COUNTIF(O:O,O2),Z2))),AB2)

'IF(调节变量="累加权重",1,IF(展示组=1,期望权重,期望权重*15))'
初始权重=IF(AF2="累加权重",1,IF(N2=1,AC2,AC2*15))

'期望权重/SUM(AC:AC) AC: 期望权重'
目标概率=AC2/SUM(AC:AC)

'1/目标概率/IF(序号>=1,COUNTIF(O:O,O2),1)'
期望次数=1/AG2/IF(R2>=1,COUNTIF(O:O,O2),1)

'IF(展示组>1,"",IF(总价="",IF(展示组=1,单价*IF(价值系数="",1,价值系数)*数量/$B$3,上一个道具的定价次数),总价/$B$3))'
定价次数=IF(N2>1,"",IF(U2="",IF(R2=1,T2*IF(Q2="",1,Q2)*J2/$B$3,AI1),U2/$B$3))

'IF(定价次数="","",INT(定价次数*必得倍率))'
验算保底=IF(AI2="","",INT(AI2*$B$4))

'IF(OR(验算保底="",展示组=2,手动保底="无保底"),"",IF(手动保底="",验算保底,手动保底))'
实际保底=IF(OR(AJ2="",N2=2,AK2="无保底"),"",IF(AK2="",AJ2,AK2))

'1/实际保底'
保底概率=1/AL2

'IF(实际保底="","",INT(实际保底*(1-$B$5))&","&INT(实际保底*(1+$B$5))) 实际保底的上下限'
浮动保底=IF(AL4="","",INT(AL4*(1-$B$5))&","&INT(AL4*(1+$B$5)))

'实际保底*2+冷却次数
INT(实际保底/4)'
抽取门槛1(特殊)=AL4*2+AP3
抽取门槛2(通用)=INT(AL4/4)

冷却次数=抽取门槛

'IF(浮动保底="",实际保底+抽取门槛-冷却次数,"")'
首次获得=IF(AN2="",AL2+AO2-AP2,"")
```

基础计算 - 大数概率
>定价概率 -> 内部概率 -> 大数概率
>分组1：
>定价概率 = 内部概率 = 大数概率
>分组2：
>在分组1的剩余概率中划分

对外概率：大数概率-奖池和

权重变换 - 抽奖感觉，提升独立成组的出货率
>特别调整：
>大数概率 -> 权重
>存在大数概率的道具，直接按照100000抽来算权重
>不存在大数概率并且独立成组的物品  引入最贵物品的总价X权重 / (价值系数X单价X数量)，提升权重
>最终稀释目标权重

实际抽奖
>权重 -> 期望权重
>权重 -> 最终概率 -> 目标概率

保底
>调整奖池1中相对低价值物品的保底，奖池2无保底，看起来奖池1前期出货率高一点

同奖励分阶段抽奖
>比如某大奖道具2个， 是1+1，第一个按照正常抽奖来，第二个则有首次获得需求，如果大佬要全拿到，一般门槛都会非常的高，导致首次获得的次数会比较大，然后国服由于有限制，会计算一个理论获得的天数，跨度不能太大，保证大佬能抽到


##### 付费活动 - 集训礼包

活动礼包： 产出活动代币、抽奖代币、特殊物料、稀有材料等
商城礼包： 商城一般投放是比较固定的，基本上都有限制

##### 付费活动 - 消费小队

吸引消费玩家抱团，提供一些消费返利

##### 付费活动 - 不放回抽奖

限时金券的主要产出来源

##### 付费活动 - 金券商店

一些非常珍稀的游戏内道具来源，以限时金券作为一般锚定物

##### 付费活动 - 商队集市

往期家具投放，游戏内一般物资产出投放（方便大佬跳过交易之城直接购买，生产需要的物品）

##### 固定投放 - 集训任务 & 商城

除开所有付费活动外，基础玩家可以体验的基础渠道